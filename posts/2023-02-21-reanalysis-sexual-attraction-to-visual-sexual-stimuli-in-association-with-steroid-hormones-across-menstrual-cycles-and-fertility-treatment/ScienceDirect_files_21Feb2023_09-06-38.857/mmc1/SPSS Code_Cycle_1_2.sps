* Encoding: UTF-8.




**** Z-transformation hormone measures ***** 

DESCRIPTIVES VARIABLES=Z1_T1_OESTR Z1_T2_OESTR Z1_T3_OESTR Z1_T4_OESTR Z2_T1_OESTR Z2_T2_OESTR Z2_T3_OESTR Z2_T4_OESTR 
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.

DESCRIPTIVES VARIABLES=Z1_T1_Prog Z1_T2_Prog Z1_T3_Prog Z1_T4_Prog Z2_T1_Prog Z2_T2_Prog Z2_T3_Prog Z2_T4_Prog 
   /SAVE
   /STATISTICS=MEAN STDDEV MIN MAX.

DESCRIPTIVES VARIABLES=Z1_T1_Test Z1_T2_Test Z1_T3_Test Z1_T4_Test Z2_T1_Test Z2_T2_Test Z2_T3_Test Z2_T4_Test 
   /SAVE
   /STATISTICS=MEAN STDDEV MIN MAX.


**** Computing change scores *****

COMPUTE Z1_T2_T1_Oestr=ZZ1_T2_OESTR-ZZ1_T1_OESTR.
COMPUTE Z1_T3_T2_Oestr=ZZ1_T3_OESTR-ZZ1_T2_OESTR.
COMPUTE Z1_T4_T3_Oestr=ZZ1_T4_OESTR-ZZ1_T3_OESTR.
EXECUTE.

DESCRIPTIVES VARIABLES=Z1_T2_T1_Oestr Z1_T3_T2_Oestr Z1_T4_T3_Oestr
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.

COMPUTE Z1_T2_T1_Prog=ZZ1_T2_PROG-ZZ1_T1_PROG.
COMPUTE Z1_T3_T2_Prog=ZZ1_T3_PROG-ZZ1_T2_PROG.
COMPUTE Z1_T4_T3_Prog=ZZ1_T4_PROG-ZZ1_T3_PROG.
EXECUTE.

DESCRIPTIVES VARIABLES=Z1_T2_T1_Prog Z1_T3_T2_Prog Z1_T4_T3_Prog
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.


COMPUTE Z1_T2_T1_TEST=ZZ1_T2_TEST-ZZ1_T1_TEST.
COMPUTE Z1_T3_T2_TEST=ZZ1_T3_TEST-ZZ1_T2_TEST.
COMPUTE Z1_T4_T3_TEST=ZZ1_T4_TEST-ZZ1_T3_TEST.
EXECUTE.

DESCRIPTIVES VARIABLES=Z1_T2_T1_TEST Z1_T3_T2_TEST Z1_T4_T3_TEST
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.


COMPUTE Z2_T2_T1_Oestr=ZZ2_T2_OESTR-ZZ2_T1_OESTR.
COMPUTE Z2_T3_T2_Oestr=ZZ2_T3_OESTR-ZZ2_T2_OESTR.
COMPUTE Z2_T4_T3_Oestr=ZZ2_T4_OESTR-ZZ2_T3_OESTR.
EXECUTE.

DESCRIPTIVES VARIABLES=Z2_T2_T1_Oestr Z2_T3_T2_Oestr Z2_T4_T3_Oestr
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.

COMPUTE Z2_T2_T1_Prog=ZZ2_T2_PROG-ZZ2_T1_PROG.
COMPUTE Z2_T3_T2_Prog=ZZ2_T3_PROG-ZZ2_T2_PROG.
COMPUTE Z2_T4_T3_Prog=ZZ2_T4_PROG-ZZ2_T3_PROG.
EXECUTE.

DESCRIPTIVES VARIABLES=Z2_T2_T1_Prog Z2_T3_T2_Prog Z2_T4_T3_Prog
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.


COMPUTE Z2_T2_T1_TEST=ZZ2_T2_TEST-ZZ2_T1_TEST.
COMPUTE Z2_T3_T2_TEST=ZZ2_T3_TEST-ZZ2_T2_TEST.
COMPUTE Z2_T4_T3_TEST=ZZ2_T4_TEST-ZZ2_T3_TEST.
EXECUTE.

DESCRIPTIVES VARIABLES=Z2_T2_T1_TEST Z2_T3_T2_TEST Z2_T4_T3_TEST
  /SAVE
  /STATISTICS=MEAN STDDEV MIN MAX.



COMPUTE Z1_T2_T1_Faces=Z1_T2_SR_Faces-Z1_T1_SR_Faces.
COMPUTE Z1_T3_T2_Faces=Z1_T3_SR_Faces-Z1_T2_SR_Faces.
COMPUTE Z1_T4_T3_Faces=Z1_T4_SR_Faces-Z1_T3_SR_Faces.
EXECUTE.

COMPUTE Z1_T2_T1_Bodies=Z1_T2_SR_Bodies-Z1_T1_SR_Bodies.
COMPUTE Z1_T3_T2_Bodies=Z1_T3_SR_Bodies-Z1_T2_SR_Bodies.
COMPUTE Z1_T4_T3_Bodies=Z1_T4_SR_Bodies-Z1_T3_SR_Bodies.
EXECUTE.

COMPUTE Z1_T2_T1_Kissing=Z1_T2_SR_Kissing-Z1_T1_SR_Kissing.
COMPUTE Z1_T3_T2_Kissing=Z1_T3_SR_Kissing-Z1_T2_SR_Kissing.
COMPUTE Z1_T4_T3_Kissing=Z1_T4_SR_Kissing-Z1_T3_SR_Kissing.
EXECUTE.

COMPUTE Z1_T2_T1_Intercourse=Z1_T2_SR_Intercourse-Z1_T1_SR_Intercourse.
COMPUTE Z1_T3_T2_Intercourse=Z1_T3_SR_Intercourse-Z1_T2_SR_Intercourse.
COMPUTE Z1_T4_T3_Intercourse=Z1_T4_SR_Intercourse-Z1_T3_SR_Intercourse.
EXECUTE.


COMPUTE Z2_T2_T1_Faces=Z2_T2_SR_Faces-Z2_T1_SR_Faces.
COMPUTE Z2_T3_T2_Faces=Z2_T3_SR_Faces-Z2_T2_SR_Faces.
COMPUTE Z2_T4_T3_Faces=Z2_T4_SR_Faces-Z2_T3_SR_Faces.
EXECUTE.

COMPUTE Z2_T2_T1_Bodies=Z2_T2_SR_Bodies-Z2_T1_SR_Bodies.
COMPUTE Z2_T3_T2_Bodies=Z2_T3_SR_Bodies-Z2_T2_SR_Bodies.
COMPUTE Z2_T4_T3_Bodies=Z2_T4_SR_Bodies-Z2_T3_SR_Bodies.
EXECUTE.

COMPUTE Z2_T2_T1_Kissing=Z2_T2_SR_Kissing-Z2_T1_SR_Kissing.
COMPUTE Z2_T3_T2_Kissing=Z2_T3_SR_Kissing-Z2_T2_SR_Kissing.
COMPUTE Z2_T4_T3_Kissing=Z2_T4_SR_Kissing-Z2_T3_SR_Kissing.
EXECUTE.

COMPUTE Z2_T2_T1_Intercourse=Z2_T2_SR_Intercourse-Z2_T1_SR_Intercourse.
COMPUTE Z2_T3_T2_Intercourse=Z2_T3_SR_Intercourse-Z2_T2_SR_Intercourse.
COMPUTE Z2_T4_T3_Intercourse=Z2_T4_SR_Intercourse-Z2_T3_SR_Intercourse.
EXECUTE.



**** Person-period dataset for GEE models cycle 1 *****

varstocases  
 /make OESTR from ZZ1_T1_OESTR ZZ1_T2_OESTR ZZ1_T3_OESTR ZZ1_T4_OESTR 
 /make PROG from ZZ1_T1_PROG ZZ1_T2_PROG ZZ1_T3_PROG ZZ1_T4_PROG  
 /make TEST from ZZ1_T1_TEST ZZ1_T2_TEST ZZ1_T3_TEST ZZ1_T4_TEST 
 /make OESTR_raw from Z1_T1_OESTR Z1_T2_OESTR Z1_T3_OESTR Z1_T4_OESTR 
 /make PROG_raw from Z1_T1_PROG Z1_T2_PROG Z1_T3_PROG Z1_T4_PROG  
 /make TEST_raw from Z1_T1_TEST Z1_T2_TEST Z1_T3_TEST Z1_T4_TEST 
 /make Sex_rating_Faces from Z1_T1_SR_Faces Z1_T2_SR_Faces Z1_T3_SR_Faces Z1_T4_SR_Faces
 /make Sex_rating_Bodies from Z1_T1_SR_Bodies Z1_T2_SR_Bodies Z1_T3_SR_Bodies Z1_T4_SR_Bodies
 /make Sex_rating_Kissing from Z1_T1_SR_Kissing Z1_T2_SR_Kissing Z1_T3_SR_Kissing Z1_T4_SR_Kissing
 /make Sex_rating_Intercourse from Z1_T1_SR_Intercourse Z1_T2_SR_Intercourse Z1_T3_SR_Intercourse Z1_T4_SR_Intercourse
 /index=time (4)
 /null keep.


**** Person-period dataset for GEE models cycle 2 *****

varstocases  
 /make OESTR from ZZ2_T1_OESTR ZZ2_T2_OESTR ZZ2_T3_OESTR ZZ2_T4_OESTR 
 /make PROG from ZZ2_T1_PROG ZZ2_T2_PROG ZZ2_T3_PROG ZZ2_T4_PROG  
 /make TEST from ZZ2_T1_TEST ZZ2_T2_TEST ZZ2_T3_TEST ZZ2_T4_TEST 
 /make OESTR_raw from Z2_T1_OESTR Z2_T2_OESTR Z2_T3_OESTR Z2_T4_OESTR 
 /make PROG_raw from Z2_T1_PROG Z2_T2_PROG Z2_T3_PROG Z2_T4_PROG  
 /make TEST_raw from Z2_T1_TEST Z2_T2_TEST Z2_T3_TEST Z2_T4_TEST 
 /make Sex_rating_Faces from Z2_T1_SR_Faces Z2_T2_SR_Faces Z2_T3_SR_Faces Z2_T4_SR_Faces
 /make Sex_rating_Bodies from Z2_T1_SR_Bodies Z2_T2_SR_Bodies Z2_T3_SR_Bodies Z2_T4_SR_Bodies
 /make Sex_rating_Kissing from Z2_T1_SR_Kissing Z2_T2_SR_Kissing Z2_T3_SR_Kissing Z2_T4_SR_Kissing
 /make Sex_rating_Intercourse from Z2_T1_SR_Intercourse Z2_T2_SR_Intercourse Z2_T3_SR_Intercourse Z2_T4_SR_Intercourse
 /index=time (4)
 /null keep.


**** Person-period dataset for GEE models cycles 1 and 2 combined ****

varstocases  
 /make OESTR from ZZ1_T1_OESTR ZZ1_T2_OESTR ZZ1_T3_OESTR ZZ1_T4_OESTR ZZ2_T1_OESTR ZZ2_T2_OESTR ZZ2_T3_OESTR ZZ2_T4_OESTR 
 /make PROG from ZZ1_T1_PROG ZZ1_T2_PROG ZZ1_T3_PROG ZZ1_T4_PROG ZZ2_T1_PROG ZZ2_T2_PROG ZZ2_T3_PROG ZZ2_T4_PROG  
 /make TEST from ZZ1_T1_TEST ZZ1_T2_TEST ZZ1_T3_TEST ZZ1_T4_TEST  ZZ2_T1_TEST ZZ2_T2_TEST ZZ2_T3_TEST ZZ2_T4_TEST  
 /make Sex_rating_Faces from Z1_T1_SR_Faces Z1_T2_SR_Faces Z1_T3_SR_Faces Z1_T4_SR_Faces Z2_T1_SR_Faces Z2_T2_SR_Faces Z2_T3_SR_Faces Z2_T4_SR_Faces
 /make Sex_rating_Bodies from Z1_T1_SR_Bodies Z1_T2_SR_Bodies Z1_T3_SR_Bodies Z1_T4_SR_Bodies Z2_T1_SR_Bodies Z2_T2_SR_Bodies Z2_T3_SR_Bodies Z2_T4_SR_Bodies
 /make Sex_rating_Kissing from Z1_T1_SR_Kissing Z1_T2_SR_Kissing Z1_T3_SR_Kissing Z1_T4_SR_Kissing Z2_T1_SR_Kissing Z2_T2_SR_Kissing Z2_T3_SR_Kissing Z2_T4_SR_Kissing
 /make Sex_rating_Intercourse from Z1_T1_SR_Intercourse Z1_T2_SR_Intercourse Z1_T3_SR_Intercourse Z1_T4_SR_Intercourse Z2_T1_SR_Intercourse Z2_T2_SR_Intercourse Z2_T3_SR_Intercourse Z2_T4_SR_Intercourse
 /index=time (8)
 /null keep.



**** Examples GEE models *****

**** Sex rating over time cycle 1 or 2 ****

GENLIN Sex_rating_Faces BY time (ORDER=ASCENDING) 
  /MODEL time INTERCEPT=YES 
     DISTRIBUTION=NORMAL LINK=IDENTITY
  /CRITERIA METHOD=FISHER(1) MAXITERATIONS=1000 MAXSTEPHALVING=50 SCALE=MLE ANALYSISTYPE=3(WALD) CILEVEL=95 LIKELIHOOD=FULL 
  /REPEATED SUBJECT=ID WITHINSUBJECT=time SORT=YES CORRTYPE=UNSTRUCTURED ADJUSTCORR=YES COVB=ROBUST MAXITERATIONS=100  
  /EMMEANS SCALE=ORIGINAL 
  /EMMEANS TABLES=time SCALE=ORIGINAL COMPARE=time CONTRAST=POLYNOMIAL PADJUST=SEQBONFERRONI
  /MISSING CLASSMISSING=EXCLUDE 
  /PRINT CPS DESCRIPTIVES MODELINFO FIT SUMMARY SOLUTION WORKINGCORR.


**** Sex rating in association with oestrogen cycle 1 or 2 - univariable model ****

GENLIN Sex_rating_Bodies BY time (ORDER=DESCENDING) WITH OESTR
  /MODEL time OESTR INTERCEPT=YES 
     DISTRIBUTION=NORMAL LINK=IDENTITY
  /CRITERIA METHOD=FISHER(1) MAXITERATIONS=1000 MAXSTEPHALVING=50 SCALE=MLE ANALYSISTYPE=3(WALD) CILEVEL=95 LIKELIHOOD=FULL 
  /REPEATED SUBJECT=ID WITHINSUBJECT=time SORT=YES CORRTYPE=UNSTRUCTURED ADJUSTCORR=YES COVB=ROBUST MAXITERATIONS=100  
  /EMMEANS SCALE=ORIGINAL 
  /EMMEANS TABLES=time SCALE=ORIGINAL COMPARE=time CONTRAST=PAIRWISE PADJUST=SEQBONFERRONI
  /MISSING CLASSMISSING=EXCLUDE 
  /PRINT CPS DESCRIPTIVES MODELINFO FIT SUMMARY SOLUTION WORKINGCORR.


**** Sex rating in association with all hormones simulataneously cycle 1 or 2 - multivariable model ****

GENLIN Sex_rating_Intercourse BY time  (ORDER=DESCENDING) WITH OESTR  PROG TEST
  /MODEL time OESTR PROG TEST INTERCEPT=YES 
     DISTRIBUTION=NORMAL LINK=IDENTITY
  /CRITERIA METHOD=FISHER(1) MAXITERATIONS=1000 MAXSTEPHALVING=50 SCALE=MLE ANALYSISTYPE=3(WALD) CILEVEL=95 LIKELIHOOD=FULL 
  /REPEATED SUBJECT=ID WITHINSUBJECT=time SORT=YES CORRTYPE=UNSTRUCTURED ADJUSTCORR=YES COVB=ROBUST MAXITERATIONS=100  
  /EMMEANS SCALE=ORIGINAL 
  /EMMEANS TABLES=time SCALE=ORIGINAL COMPARE=time CONTRAST=PAIRWISE PADJUST=SEQBONFERRONI
  /MISSING CLASSMISSING=EXCLUDE 
  /PRINT CPS DESCRIPTIVES MODELINFO FIT SUMMARY SOLUTION WORKINGCORR.



*****************************************
*** Intra-Individual Change Models***
******************************************

**** Person-period dataset for GEE models cycle 1 ****

varstocases  
 /make Change_OESTR from ZZ1_T2_T1_OESTR ZZ1_T3_T2_OESTR ZZ1_T4_T3_OESTR 
 /make Change_PROG from ZZ1_T2_T1_PROG ZZ1_T3_T2_PROG ZZ1_T4_T3_PROG 
 /make Change_TEST from ZZ1_T2_T1_TEST ZZ1_T3_T2_TEST ZZ1_T4_T3_TEST 
 /make Change_Faces from Z1_T2_T1_Faces Z1_T3_T2_Faces Z1_T4_T3_Faces
 /make Change_Bodies from Z1_T2_T1_Bodies Z1_T3_T2_Bodies Z1_T4_T3_Bodies
 /make Change_Kissing from Z1_T2_T1_Kissing Z1_T3_T2_Kissing Z1_T4_T3_Kissing
 /make Change_Intercourse from Z1_T2_T1_Intercourse Z1_T3_T2_Intercourse Z1_T4_T3_Intercourse
 /index=time (3)
 /null keep.


**** Person-period dataset for GEE models cycle 2 ****

varstocases  
 /make Change_OESTR from ZZ2_T2_T1_OESTR ZZ2_T3_T2_OESTR ZZ2_T4_T3_OESTR 
 /make Change_PROG from ZZ2_T2_T1_PROG ZZ2_T3_T2_PROG ZZ2_T4_T3_PROG 
 /make Change_TEST from ZZ2_T2_T1_TEST ZZ2_T3_T2_TEST ZZ2_T4_T3_TEST 
 /make Change_Faces from Z2_T2_T1_Faces Z2_T3_T2_Faces Z2_T4_T3_Faces
 /make Change_Bodies from Z2_T2_T1_Bodies Z2_T3_T2_Bodies Z2_T4_T3_Bodies
 /make Change_Kissing from Z2_T2_T1_Kissing Z2_T3_T2_Kissing Z2_T4_T3_Kissing
 /make Change_Intercourse from Z2_T2_T1_Intercourse Z2_T3_T2_Intercourse Z2_T4_T3_Intercourse
 /index=time (3)
 /null keep.



**** Sex rating in association with oestrogen cycle 1 or 2 - univariable intraindividual change model ****

GENLIN Change_Intercourse BY time (ORDER=DESCENDING) WITH Change_OESTR 
  /MODEL time Change_OESTR INTERCEPT=YES 
     DISTRIBUTION=NORMAL LINK=IDENTITY
  /CRITERIA METHOD=FISHER(1) MAXITERATIONS=1000 MAXSTEPHALVING=50 SCALE=MLE ANALYSISTYPE=3(WALD) CILEVEL=95 LIKELIHOOD=FULL 
  /REPEATED SUBJECT=ID WITHINSUBJECT=time SORT=YES CORRTYPE=INDEPENDENT ADJUSTCORR=YES COVB=ROBUST MAXITERATIONS=100  
  /MISSING CLASSMISSING=EXCLUDE 
  /PRINT CPS DESCRIPTIVES MODELINFO FIT SUMMARY SOLUTION WORKINGCORR.


**** Sex rating in association with all hormones simultaneously cycle 1 or 2 - multivariable intraindividual change model ****

GENLIN Change_Kissing BY time (ORDER=DESCENDING) WITH Change_OESTR Change_PROG Change_TEST 
  /MODEL time Change_OESTR Change_PROG Change_TEST INTERCEPT=YES 
     DISTRIBUTION=NORMAL LINK=IDENTITY
  /CRITERIA METHOD=FISHER(1) MAXITERATIONS=1000 MAXSTEPHALVING=50 SCALE=MLE ANALYSISTYPE=3(WALD) CILEVEL=95 LIKELIHOOD=FULL 
  /REPEATED SUBJECT=ID WITHINSUBJECT=time SORT=YES CORRTYPE=INDEPENDENT ADJUSTCORR=YES COVB=ROBUST MAXITERATIONS=100  
  /MISSING CLASSMISSING=EXCLUDE 
  /PRINT CPS DESCRIPTIVES MODELINFO FIT SUMMARY SOLUTION WORKINGCORR.













